cmake_minimum_required (VERSION 3.13)

if (NOT EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/nanogui/CMakeLists.txt")
  message (FATAL_ERROR
           "Some tev dependencies are missing. "
           "If you forgot the '--recursive' flag when cloning this project, "
           "this can be fixed by calling 'git submodule update --init --recursive'")
endif()

# Compile nanogui
set (NANOGUI_BUILD_EXAMPLES OFF CACHE BOOL " " FORCE)
set (NANOGUI_BUILD_SHARED OFF CACHE BOOL " " FORCE)
set (NANOGUI_BUILD_PYTHON OFF CACHE BOOL " " FORCE)
set (NANOGUI_INSTALL OFF CACHE BOOL " " FORCE)
add_subdirectory (nanogui)

set (NANOGUI_TARGETS nanogui glfw glfw_objects)
set_property (TARGET ${NANOGUI_TARGETS} PROPERTY FOLDER "dependencies")

if (WIN32)
  # Compile zlib (only on Windows)
  set (ZLIB_BUILD_STATIC_LIBS ON CACHE BOOL " " FORCE)
  set (ZLIB_BUILD_SHARED_LIBS OFF CACHE BOOL " " FORCE)
  set (SKIP_INSTALL_ALL ON CACHE BOOL " " FORCE)
  add_subdirectory (zlib)
  set_property (TARGET zlibstatic PROPERTY FOLDER "dependencies")
  set (ZLIB_INCLUDE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/zlib" CACHE PATH " " FORCE)
  set (ZLIB_LIBRARY zlibstatic)
  include_directories (${ZLIB_INCLUDE_DIR} "${CMAKE_CURRENT_BINARY_DIR}/zlib")

  # Compile DirectXTex (only on Windows)
  set (BUILD_TOOLS OFF CACHE BOOL " " FORCE)
  set (BUILD_DX11 OFF CACHE BOOL " " FORCE)
  set (BUILD_DX12 OFF CACHE BOOL " " FORCE)
  set (BC_USE_OPENMP OFF CACHE BOOL " " FORCE)
  add_subdirectory (DirectXTex)
  set_property (TARGET DirectXTex PROPERTY FOLDER "dependencies")

  set (DIRECTXTEX_INCLUDE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/DirectXTex/DirectXTex" CACHE PATH " " FORCE)
  set (DIRECTXTEX_LIBRARY DirectXTex)

  include_directories (${DIRECTXTEX_INCLUDE_DIR})
endif()

# Compile OpenEXR
set (PYILMBASE_ENABLE OFF CACHE BOOL " " FORCE)
set (ILMBASE_BUILD_SHARED_LIBS OFF CACHE BOOL " " FORCE)
set (OPENEXR_BUILD_SHARED_LIBS OFF CACHE BOOL " " FORCE)
set (ILMBASE_NAMESPACE_VERSIONING OFF CACHE BOOL " " FORCE)
set (OPENEXR_NAMESPACE_VERSIONING OFF CACHE BOOL " " FORCE)
add_subdirectory (openexr EXCLUDE_FROM_ALL)
set_property (TARGET Half Iex IlmImf IlmThread Imath PROPERTY FOLDER "dependencies")

# Compile clip
set (CLIP_EXAMPLES OFF CACHE BOOL " " FORCE)
set (CLIP_TESTS OFF CACHE BOOL " " FORCE)
add_subdirectory (clip)

# Manually populate locations of our included dependencies.
set (ARGS_INCLUDE ${CMAKE_CURRENT_SOURCE_DIR}/args PARENT_SCOPE)
set (CLIP_INCLUDE ${CMAKE_CURRENT_SOURCE_DIR}/clip PARENT_SCOPE)

if (NOT ${CMAKE_SYSTEM_NAME} MATCHES "Emscripten")
  set (GLFW_INCLUDE ${CMAKE_CURRENT_SOURCE_DIR}/nanogui/ext/glfw/include PARENT_SCOPE)
endif()

set (NANOGUI_INCLUDE ${CMAKE_CURRENT_SOURCE_DIR}/nanogui/ext/nanovg/src
                     ${CMAKE_CURRENT_SOURCE_DIR}/nanogui/include PARENT_SCOPE)
set (NANOGUI_EXTRA_INCS ${NANOGUI_EXTRA_INCS} PARENT_SCOPE)
set (NANOGUI_EXTRA_DEFS ${NANOGUI_EXTRA_DEFS} PARENT_SCOPE)
set (NANOGUI_EXTRA_LIBS ${NANOGUI_EXTRA_LIBS} PARENT_SCOPE)
set (OPENEXR_INCLUDE_DIRS
     ${CMAKE_CURRENT_SOURCE_DIR}/openexr/IlmBase/Imath
     ${CMAKE_CURRENT_SOURCE_DIR}/openexr/IlmBase/Iex
     ${CMAKE_CURRENT_SOURCE_DIR}/openexr/IlmBase/Half
     ${CMAKE_CURRENT_SOURCE_DIR}/openexr/OpenEXR/IlmImf
     ${CMAKE_CURRENT_BINARY_DIR}/openexr/OpenEXR/config
     ${CMAKE_CURRENT_BINARY_DIR}/openexr/IlmBase/config PARENT_SCOPE)
set (QOI_INCLUDE ${CMAKE_CURRENT_SOURCE_DIR}/qoi PARENT_SCOPE)
set (STB_INCLUDE ${CMAKE_CURRENT_SOURCE_DIR}/stb PARENT_SCOPE)
set (TINYFORMAT_INCLUDE ${CMAKE_CURRENT_SOURCE_DIR}/tinyformat PARENT_SCOPE)
set (TINYLOGGER_INCLUDE ${CMAKE_CURRENT_SOURCE_DIR}/tinylogger PARENT_SCOPE)
set (UTFCPP_INCLUDE ${CMAKE_CURRENT_SOURCE_DIR}/utfcpp/source PARENT_SCOPE)
